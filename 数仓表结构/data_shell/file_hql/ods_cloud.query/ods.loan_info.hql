set spark.executor.memory=4g;
set spark.executor.memoryOverhead=4g;
set spark.shuffle.memoryFraction=0.6;        -- shuffle操作的内存占比
set spark.maxRemoteBlockSizeFetchToMem=200m;
set hive.auto.convert.join=false;            -- 关闭自动 MapJoin
set hive.support.quoted.identifiers=None;

insert overwrite table ods_new_s.loan_info partition(is_settled = 'no',product_id)
select
  due_bill_no,
  apply_no,
  loan_active_date,
  loan_init_principal,
  account_age,
  loan_init_term,
  loan_term,
  if(e_d_date = '3000-12-31' and should_repay_date is null,
   lag(should_repay_date) over (partition by due_bill_no order by biz_date),
   should_repay_date
    ) as should_repay_date,
  loan_term_repaid,
  loan_term_remain,
  loan_init_interest,
  loan_init_term_fee,
  loan_init_svc_fee,
  loan_status,
  loan_status_cn,
  loan_out_reason,
  paid_out_type,
  paid_out_type_cn,
  paid_out_date,
  terminal_date,
  paid_amount,
  paid_principal,
  paid_interest,
  paid_penalty,
  paid_svc_fee,
  paid_term_fee,
  paid_mult,
  remain_amount,
  remain_principal,
  remain_interest,
  remain_svc_fee,
  remain_term_fee,
  remain_othAmounts,
  overdue_principal,
  overdue_interest,
  overdue_svc_fee,
  overdue_term_fee,
  overdue_penalty,
  overdue_mult_amt,
  min(overdue_date_start) over(partition by due_bill_no order by biz_date) as overdue_date_first,
  overdue_date_start,
  overdue_days,
  overdue_date,
  overdue_date_start as dpd_begin_date,
  overdue_days as dpd_days,
  0 as dpd_days_count,
  dpd_days_max,
  collect_out_date as collect_out_date,
  overdue_term,
  overdue_terms_count,
  overdue_terms_max,
  overdue_principal_accumulate,
  overdue_principal_max,
  biz_date                                                                                     as s_d_date,
  nvl(lead(biz_date) over(partition by product_id,due_bill_no order by biz_date),'3000-12-31') as e_d_date,
  cast(biz_date as timestamp) as effective_time,
  cast(nvl(lead(biz_date) over(partition by product_id,due_bill_no order by biz_date),'3000-12-31') as timestamp) as expire_time,
  product_id
from ods_new_s.loan_info_inter;
